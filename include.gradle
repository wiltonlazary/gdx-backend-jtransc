// http://jedicoder.blogspot.com.es/2011/11/automated-gradle-project-deployment-to.html
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'signing'

group = "${project.ext.group}"
version = "${project.ext.mediaVersion}"

repositories {
	mavenLocal()
	mavenCentral()
	maven {
		url "https://dl.bintray.com/jtransc/jtransc/"
	}
}

sourceSets {
	main.java.srcDirs = [ 'src' ]
	main.resources.srcDirs = [ 'resources' ]
	test.java.srcDirs = [ 'test' ]
	test.resources.srcDirs = [ 'testresources' ]
}

dependencies {
	compile "com.jtransc:jtransc-rt-core:${project.ext.jtranscVersion}"
	compile "com.badlogicgames.gdx:gdx:${project.ext.gdxVersion}"
	testCompile 'junit:junit:4.12'
}

if (System.getenv("CI") == null) {
	task javadoc2(type: Javadoc) {
		failOnError = false
	}

	task javadocJar(type: Jar, dependsOn: javadoc2) {
		classifier = 'javadoc'
		from 'build/docs/javadoc'
	}

	task sourcesJar(type: Jar) {
		from sourceSets.main.allSource
		classifier = 'sources'
	}

	artifacts {
		archives jar
		archives javadocJar
		archives sourcesJar
	}

	signing {
		sign configurations.archives
	}

	uploadArchives {
		repositories {
			mavenDeployer {
				beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

				repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
					authentication(userName: sonatypeUsername, password: sonatypePassword)
				}

				pom.project {
					name "${project.name}"
					packaging 'jar'
					description 'JVM AOT compiler currently generating JavaScript, Haxe, with initial focus on Kotlin and games.'
					url 'https://github.com/jtransc/jtransc/'
					inceptionYear '2015'

					scm {
						url 'scm:git@github.com:jtransc/jtransc.git'
						connection 'scm:git@github.com:jtransc/jtransc.git'
						developerConnection 'scm:git@github.com:jtransc/jtransc.git'
					}

					licenses {
						license {
							name 'The Apache Software License, Version 2.0'
							url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
							distribution 'repo'
						}
					}

					developers {
						developer {
							id 'soywiz'
							name 'Carlos Ballesteros Velasco'
						}
					}
				}
			}
		}
	}

//println("uploadArchives!: ${project.name}")
}


def props = rootProject.extensions.getExtraProperties()

if (!props.has("gitVersion")) {
	try {
		props.set("gitVersion", "git describe".execute().text.trim())
	} catch (Throwable) {
		props.set("gitVersion", "${jtranscVersion}")
	}
}

//println(props.get("gitVersion"))

publishing {
	publications {
		MyPublication(MavenPublication) {
			from components.java
			groupId project.group
			artifactId project.name
			version props.get("gitVersion")
			//version "$project.version"
		}
	}
}

bintray {
	user = System.getenv("BINTRAY_USER") ?: properties['BINTRAY_USER'] ?: rootProject.properties['BINTRAY_USER']
	key = System.getenv("BINTRAY_KEY") ?: properties['BINTRAY_USER'] ?: rootProject.properties['BINTRAY_KEY']

	publications = ['MyPublication']

	dryRun = false
	publish = true
	override = true

	pkg {
		userOrg = 'jtransc'
		repo = 'jtransc'
		name = 'jtransc'
		//name = rootProject.name
		licenses = ['Apache-2.0']
		vcsUrl = 'https://github.com/jtransc/jtransc.git'
	}
}
